apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'com.tease.androidactionstest'
def LIB_ARTIFACT_ID = 'androidactionstest'
def LIB_VERSION = '1.1.6';

publishing {
	repositories {
		maven {
			name = "GithubPackages"
			url = uri("https://maven.pkg.github.com/vlas-ilya/AndroidActionsTest")
			credentials {
				username = System.getenv('GITHUB_USER') ?: project.properties['GITHUB_USER']
				password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: project.properties['GITHUB_PERSONAL_ACCESS_TOKEN']
			}
		}
		maven {
			name = 'CustomMavenRepo'
			url = "file://${buildDir}/repo"
		}
	}
	publications {
		android.applicationVariants.all { variant ->
			variant.outputs.each { output ->
				"maven${variant.name.capitalize()}Apk"(MavenPublication) {
					groupId LIB_GROUP_ID
					artifactId LIB_ARTIFACT_ID
					version LIB_VERSION

					artifact(output.outputFile)
				}
			}
		}
	}
}

task cleanBuildTestPublish {
	dependsOn 'clean'
	dependsOn 'assemble'
	dependsOn 'publish'
	tasks.findByName('assemble').mustRunAfter 'clean'
	tasks.findByName('publish').mustRunAfter 'assemble'
}
